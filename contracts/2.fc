{-
  TASK 2 - Matrix multiplier
  Write the method that multiplies one matrix by another.
  The 1st and 2nd parameter of the function will contain
  a matrix implemented using tuples (eg: [[1,2], [3,4]]).
  Matrices can be of different sizes, ranging from 4x4
  up to 32x32.
  Matrix can be rectangular where matrixA must to be of size n*m & matrixB of size m*p.
  After calculating the result matrix, the function should return final result (as tuple).
-}
int tuple_length (tuple t) asm "TLEN";

() recv_internal() {
}

;; testable
(tuple) matrix_multiplier(tuple matrixA, tuple matrixB) method_id {
    tuple res = empty_tuple();
    int n = matrixA.tuple_length();
    int m = matrixB.tuple_length();
    int p = matrixB.first().tuple_length();
    int i = 0;
    int j = 0;
    int k = 0;
    while(i < n){
        tuple X = empty_tuple();
        j = 0;
        while(j < p){
            int x = 0;
            k = 0;
            while(k < m){
                x = x + matrixA.at(i).at(k) * matrixB.at(k).at(j);
                k = k + 1;
            }
            X~tpush(x);
            j = j + 1;
        }
        res~tpush(X);
        i = i + 1;
    }
    return res;
}
